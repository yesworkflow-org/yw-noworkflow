

nw_script_activation(Script, Command, ScriptActId, Docstring) :-
    trial(_,_,_, Script, _, Command, _,_,_, Docstring),
    activation(_, ScriptActId, _,_,_,_, nil).

nw_function_definition(FuncId, FuncName, FirstLine, LastLine, Docstring) :-
    function_def(_, FuncId, FuncName, _, FirstLine, LastLine, Docstring).

nw_function_activation(ActId, FuncId, FuncName, CallerLine, CallerActivationId) :-
    activation(_, ActId, FuncName, CallerLine, _, _, CallerActivationId),
    nw_function_definition(FuncId, FuncName, _, _, _).
nw_function_activation(ActId, nil, FuncName, CallerLine, CallerActivationId) :-
    activation(_, ActId, FuncName, CallerLine, _, _, CallerActivationId),
    not nw_function_definition(_, FuncName, _, _, _).


nw_function_argument(ActId, FuncName, LocalVarId, LocalVarName, Value, variable, CallerVarName, CallerVarId) :-
    nw_function_argument_variable(ActId, FuncName, LocalVarId, LocalVarName, Value, CallerVarId, CallerVarName),
    CallerVarId \== nil.
nw_function_argument(ActId, FuncName, LocalVarId, LocalVarName, Value, literal, CallerVarName, CallerVarId) :-
    nw_function_argument_literal(ActId, FuncName, LocalVarId, LocalVarName, Value),
    CallerVarId = nil,
    CallerVarName = nil.

nw_function_argument_variable(ActId, FuncName, LocalVarId, LocalVarName, Value, CallerVarId, CallerVarName) :-
    activation(_, ActId, FuncName, _,_,_,_),
    object_value(_, ActId, _, LocalVarName, _, 'ARGUMENT'),
    variable(_, ActId, LocalVarId, LocalVarName, _, Value, _),
    dependency(_,_, ActId, LocalVarId, CallerActivationId, CallerVarId),
    variable(_, CallerActivationId, CallerVarId, CallerVarName, _, Value,_).

nw_function_argument_literal(ActId, FuncName, LocalVarId, LocalVarName, Value) :-
    activation(_, ActId, FuncName, _, _, _, _),
    object_value(_, ActId, _, LocalVarName, _, 'ARGUMENT'),
    variable(_, ActId, LocalVarId, LocalVarName, AssignmentLine, Value, _),
    nw_function_definition(_, FuncName, FuncDefLine, _, _),
    AssignmentLine = FuncDefLine,
    not nw_function_argument_variable(ActId, FuncName, _, LocalVarName, Value, _,_).

nw_variable_assignment(ActId, VariableId, VariableName, Line, Value) :-
    variable(_, ActId, VariableId, VariableName, Line, Value, _),
    not nw_variable_is_function(VariableId),
    Line \== 0,
    VariableName \== 'return',
    Value \= 'now(n/a)'.

nw_variable_is_function(VariableId) :-
    variable(_, VariableId, _, Name, Line, _, _),
    function_def(_,_, Name, _, Line, _,_).

nw_variable_usage(UsageId, ActId, VariableId, VariableName, VariableValue, Line) :-
    usage(_, ActId, VariableId, UsageId, VariableName, Line),
    variable(_,_, VariableId, VariableName, _, VariableValue, _),
    not nw_usage_is_function_call(UsageId, VariableId).

nw_usage_is_function_call(UsageId, VariableId) :-
    usage(_, _, VariableId, UsageId, Name, Line),
    activation(_,_, Name, Line, _,_,_).

:- table nw_variable_dependency/8.
nw_variable_dependency(Why, ActId, FuncName, AssignmentLine, DownstreamVarId, DownstreamVarName, UpstreamVarId, UpstreamVarName) :-
    dependency(_, _, ActId, DownstreamVarId, ActId, UpstreamVarId),
    nw_variable_assignment(ActId, DownstreamVarId, DownstreamVarName, AssignmentLine, _),
    nw_variable_usage(_, ActId, UpstreamVarId, UpstreamVarName, _, _),
    nw_function_activation(ActId, _, FuncName, _, _),
    Why = assignment.
